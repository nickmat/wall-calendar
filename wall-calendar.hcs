// WallCalendar.hcs  Write a given month, year as wall calendar in html

function html_month( year, month ) {

let mstart = date {"g",year,month,1};
let start = mstart - mstart[wday] + 1;
let mname = convert,m month;
let image = convert,:lp:03 month;
let nmoon = start[newmoon];
let nmoon2 = (nmoon + 27)[newmoon];

let nl = "
";

result = "<!DOCTYPE html>
<!-- Written by HistoryCal script WallCalendar.hcs " + string today + " -->
<html lang='en'>
<head>
<meta charset='utf-8'>
<title>Calendar</title>
<style>
img {margin-left: 1%; margin-bottom: 1em;}
table {
 table-layout: fixed;
 width: 98%;
 margin-left: 1%;
 border-collapse: collapse;
}
th {border: 1px solid black; background-color: #fff5cc;}
th.top {font-size: 200%;}
td {
 width: 14%;
 height: 6em;
 border: 1px solid black;
 padding: 0 0.3em;
 vertical-align: top;
 font-size: 120%; font-weight: bold;
}
td.light {color: darkgrey; background-color: snow;}
.small { font-size: small;}
</style>
</head>
<body>
<img src='image/" + image + ".jpg' alt='Image for " + mname + "' width='98%'>
<table>
<tr><th colspan='7' class='top'>" + convert,m month + " " + year + "</th></tr>
<tr>" + nl;

let i = 1;
do while i < 8
    result += "<th>" + convert,w:a i + "</th>" + nl;
    i += 1;
loop
result += "</tr>" + nl;

let jdn = start;
let i = 0;
let class = "light";
do while i < 6
    result += "<tr>" + nl;
    let j = 0;
    do while j < 7
        let d = jdn[day];
        if d = 1
            // Swap class between light and bold.
            class = @if( class = "bold", "light", "bold" );
        endif
        result += "<td class='" + class + "'>" + d;
        let note = "";
        if j = 2 and class = "bold"
            let wn = jdn / 7;
            if wn mod 2 = 0
                note += " â™»ï¸";
            else
                note += " â¬›";
            endif
        endif
        if jdn = nmoon or jdn = nmoon2
            note += " ðŸŒ‘";
        endif
        if note[.size] > 0
            result += "<scan class='small'>" + note + "</scan>";
        endif
        result += "</td>" + nl;
        jdn += 1;
        j += 1;    
    loop
    result += "</tr>" + nl;
    i += 1;
loop
result += "</table>
<p class='small'>Created with <a href='http://historycal.org'>HistoryCal</a> Script</p>
</body>
</html>";
}

writeln  @html_month( 2020, 5 );

end;
